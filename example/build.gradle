
apply plugin: 'java'

sourceSets {
    main {
        java {
            srcDir 'generated'
        }
    }
}

clean {
    delete "${projectDir}/generated"
    println "Deleting ${projectDir}/generated"
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

task testSourcesJar(type: Jar) {
    from sourceSets.test.allSource
    classifier = 'test-sources'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}


artifacts {
    archives sourcesJar
    archives javadocJar
    archives testSourcesJar
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.8.1'
}

if (hasProperty("publish") && !ext.publish.equals("false")) {
    signing {
        sign configurations.archives
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: version.endsWith('RELEASE') ? 'https://oss.sonatype.org/service/local/staging/deploy/maven2' : 'https://oss.sonatype.org/content/repositories/snapshots') {
                    authentication(userName: sonatypeUsername, password: sonatypePassword)
                }

                pom.project {
                    name 'Buildable Examples with Tests'
                    artifactId 'example'
                    packaging 'jar'
                    description 'Examples of @Buildable use and tests'
                    url 'http://incandescent.bradneighbors.com/'

                    parent {
                        groupId 'org.sonatype.oss'
                        artifactId 'oss-parent'
                        version '7'
                    }

                    scm {
                        url 'https://github.com/brad-neighbors/buildable/'
                        connection 'scm:git:git@github.com:brad-neighbors/buildable.git'
                        developerConnection 'scm:git:git@github.com:brad-neighbors/buildable.git'
                    }

                    licenses {
                        license {
                            name 'The Apache Software License, Version 2.0'
                            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                            distribution 'repo'
                        }
                    }

                    developers {
                        developer {
                            id 'bneighbors'
                            name 'Brad Neighbors'
                            email 'brad.r.neighbors@gmail.com'
                            url 'http://incandescent.bradneighbors.com/'
                            timezone '-6'
                            roles {
                                role 'lead'
                            }
                        }
                        developer {
                            id 'bduong'
                            name 'Ben Duong'
                            email 'benjaminf.duong@gmail.com'
                            timezone '-6'
                        }
                    }
                }
            }
        }
    }
}



